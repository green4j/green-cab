plugins {
    id 'java'
    id 'jacoco'
}

group = projectGroup
version = projectVersion

subprojects {
    apply plugin: 'java'
}

allprojects {
    apply plugin: 'checkstyle'

    sourceCompatibility = projectSourceCompatibility
    targetCompatibility = projectSourceCompatibility

    repositories {
        mavenCentral()
    }

    checkstyle {
        toolVersion = '8.42'
    }
}

dependencies {
    testImplementation 'org.junit.jupiter:junit-jupiter-api:5.7.2'
    testRuntimeOnly 'org.junit.jupiter:junit-jupiter-engine:5.7.2'
}

test {
    testLogging {
        showStandardStreams = true
        exceptionFormat = 'full'
    }
}

jar {
    manifest {
        attributes 'Implementation-Title': projectName,
                   'Implementation-Version': projectVersion,
                   'Implementation-Vendor' : projectAuthor
    }
}

javadoc {
    title = "<h1>$projectName</h1>"
    options.bottom = "<i>Copyright &#169; 2019 $projectAuthor.</i>"
    if (JavaVersion.current() == JavaVersion.VERSION_1_10) {
        options.addBooleanOption 'html5', true
    }
}

jacoco {
    toolVersion = "0.8.7"
}

jacocoTestReport {
    reports {
        xml.enabled true
        html.enabled true
    }
}

check.dependsOn jacocoTestReport

wrapper {
    gradleVersion = '7.0.2'
    distributionType = 'ALL'
}
